
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	//++ Сараева 16.05.25
	ЭлементКонтактноеЛицо = Элементы.Добавить("ДорабПрил_КонтактноеЛицо", Тип("ПолеФормы"), Элементы.ГруппаШапкаПраво);
	ЭлементКонтактноеЛицо.ПутьКДанным = "Объект.ДорабПрил_КонтактноеЛицо";
	ЭлементКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементКонтактноеЛицо.Заголовок = "Контактное лицо";
    //-- Сараева 16.05.25
        
	//++ Сараева 18.05.25
	ГруппаСкидки = Элементы.Добавить("ГруппаСкидки", Тип("ГруппаФормы"), Элементы.ГруппаШапкаЛево);
	ГруппаСкидки.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидки.ОтображатьЗаголовок = Ложь;
	ГруппаСкидки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ЭлементСогласованнаяСкидка = Элементы.Добавить("ДорабПрил_СогласованнаяСкидка", Тип("ПолеФормы"), Элементы.ГруппаСкидки);
	ЭлементСогласованнаяСкидка.ПутьКДанным = "Объект.ДорабПрил_СогласованнаяСкидка";
	ЭлементСогласованнаяСкидка.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементСогласованнаяСкидка.Заголовок = "Согласованная скидка";
	Элементы.ДорабПрил_СогласованнаяСкидка.УстановитьДействие("ПриИзменении", "ДорабПрил_СогласованнаяСкидкаПриИзменении");
	
	КомандаПересчитать = Команды.Добавить("ПересчитатьТаблицу");
	КомандаПересчитать.Заголовок = "Пересчитать таблицу";
	КомандаПересчитать.Действие = "КомандаПересчитатьТаблицу"; 
	
	КнопкаКомандыПересчитать = Элементы.Добавить("КнопкаПересчитатьТаблицу", Тип("КнопкаФормы"), Элементы.ГруппаСкидки);
	КнопкаКомандыПересчитать.ИмяКоманды = "ПересчитатьТаблицу";
	КнопкаКомандыПересчитать.Отображение = ОтображениеКнопки.КартинкаИТекст;
	КнопкаКомандыПересчитать.Картинка = БиблиотекаКартинок.Обновить;
	КнопкаКомандыПересчитать.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	//-- Сараева 18.05.25
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

//++ Сараева 18.05.25
&НаКлиенте
Процедура ДорабПрил_СогласованнаяСкидкаПриИзменении(Элемент)
	
	Если Объект.Товары.Количество() = 0 И Объект.Услуги.Количество() = 0 Тогда
		Возврат
	КонецЕсли;
	
	ЗадатьВопросПользователю();
	
КонецПроцедуры


//-- Сараева 18.05.25

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Процедура ЗадатьВопросПользователю()
	
	Обещание = ВопросАсинх("Изменен процент скидки. Пересчитать таблицы товаров и услуг?", РежимДиалогаВопрос.ДаНет);
	ОтветНаВопрос = Ждать Обещание;
	
	Если ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	РасчитатьСуммуСУчетомСкидки();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	//++ Сараева 18.05.25
	//КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	//ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	ТекущиеДанные.Сумма = СуммаСУчетомСкидки(ТекущиеДанные); 
	РассчитатьСуммуДокумента();
	//-- Сараева 18.05.25
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

//++Сараева 18.05.25
&НаКлиенте
Процедура РасчитатьСуммуСУчетомСкидки()
	
	Для Каждого Строка Из Объект.Товары Цикл
		
		Строка.Сумма = СуммаСУчетомСкидки(Строка);
		
	КонецЦикла; 
	
	Для Каждого Строка Из Объект.Услуги Цикл
		
		Строка.Сумма = СуммаСУчетомСкидки(Строка);
		
	КонецЦикла; 
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры 

&НаКлиенте
Функция СуммаСУчетомСкидки(ИзмененнаяСтрока)
	
	Если ИзмененнаяСтрока.Скидка + Объект.ДорабПрил_СогласованнаяСкидка > 100 Тогда
		Сообщить(СтрШаблон("Общая скидка в строке №%1 по номенклатуре %2 превышает 100%%",
		ИзмененнаяСтрока.НомерСтроки, ИзмененнаяСтрока.Номенклатура));
		ИзмененнаяСтрока.Сумма = 0;
	Иначе
		КоэффициентСкидки = 1 - (ИзмененнаяСтрока.Скидка + Объект.ДорабПрил_СогласованнаяСкидка) / 100; 
		ИзмененнаяСтрока.Сумма = ИзмененнаяСтрока.Цена * ИзмененнаяСтрока.Количество * КоэффициентСкидки;
	КонецЕсли; 
	
	Возврат ИзмененнаяСтрока.Сумма;
	
КонецФункции
//--Сараева 18.05.25

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

//++Сараева 18.05.25
&НаКлиенте
Процедура КомандаПересчитатьТаблицу(Команда) 
	
	Если Объект.Товары.Количество() = 0 И Объект.Услуги.Количество() = 0 Тогда
		Возврат
	КонецЕсли;
	
	РасчитатьСуммуСУчетомСкидки();
	
КонецПроцедуры 
//--Сараева 18.05.25

#КонецОбласти

#КонецОбласти
